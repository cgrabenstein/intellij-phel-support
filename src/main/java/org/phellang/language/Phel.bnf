{
  generate=[tokenAccessors="no"]
  parserClass='org.phellang.language.parser.PhelParser'
  psiPackage='org.phellang.language.psi'
  psiImplPackage='org.phellang.language.psi.impl'

  psiClassPrefix='Phel'
  psiImplPackage="org.phellang.language.psi.impl"

  elementTypeHolderClass="org.phellang.language.psi.PhelTypes"
  elementTypeClass="org.phellang.language.psi.PhelElementType"
  tokenTypeClass="org.phellang.language.psi.PhelTokenType"

  tokens=[
    whitespace='regexp:\s+'
    line_comment='regexp:#.*'
    string='regexp:"([^"]|\\")*"'
    number='regexp:\d+(\.\d*)?([eE][+-]?\d+)?'
    hexnum='regexp:[+-]?0x[\da-fA-F_]+'
    binnum='regexp:[+-]?0b[01_]+'
    octnum='regexp:[+-]?0o[0-7_]+'
    char='regexp:\\(u\d{4}|newline|space|backspace|return|.)'
    bool="regexp:true|false"
    nil='nil'
    nan='NAN'
    sym="regexp:[\w.<>$%&=*/+\-!?_'[^\d]][\w.<>$%&=*/+\-!?_']*((:[\w<>$%&=*/+\-!?_'])+)?"

    paren1='('
    paren2=')'
    bracket1='['
    bracket2=']'
    brace1='{'
    brace2='}'
    colon=':'
    coloncolon='::'
    comma=','
    comma_at=',@'
    quote="'"
    syntax_quote="`"

    hat="^"
    tilde="~"
    tilde_at="~@"

    dot='.'
    dotdash='.-'
    slash='/'
    
    // Multi-character PHP operators
    and_and='&&'
    or_or='||'
    shift_left='<<'
    shift_right='>>'
    not_equal='!='
    not_identical='!=='
    increment='++'
    decrement='--'
    
    keyword_token='KEYWORD'
  ]
  extends("p_form|s_form")=form
  extends("map|l_v_form")=p_form
  extends("vec|list")=l_v_form

  extends("symbol|keyword|literal|access")=s_form

  extends("symbol_.*")=symbol
  elementType("symbol_.*")=symbol
  elementType("access_.*")=access
  pin("list|vec|map")="'[\(\[\{]'"

  name("metadata")=form

  methods("reader_macro|metadata")=[toString]
  consumeTokenMethod("symbol_nsq")="fast"
}

root ::= form *
private root_entry ::= not_eof form  
private not_eof ::= !<<eof>>

form ::= form_prefix form_prefix * form_upper | form_inner
  {pin(".*")=1 methods=[form="" metas="metadata" readerMacros="reader_macro" toString]}
private form_prefix ::= metadata | reader_macro

upper form_upper ::= form_inner {elementType=form name=form}
private form_inner ::= p_forms | s_forms
private p_forms ::= list | vec | map
private s_forms ::= symbol access_left? | keyword | literal | access

fake p_form ::= form * {methods=[forms="form"]}
fake l_v_form ::=
fake s_form ::=
list ::= '(' list_body ')'
  {methods=[getTextOffset getFirst]}
vec ::= '[' vec_body ']'
map ::= '{' map_body '}'
symbol ::= symbol_qualified
  {extends="org.phellang.language.psi.PhelNamedElementImpl"
   implements="com.intellij.psi.PsiNameIdentifierOwner"
   methods=[getName setName getNameIdentifier getTextOffset getReference]}
keyword ::= keyword_token | (':' | '::') symbol_qualified
private symbol_qualified ::= symbol_plain symbol_nsq?
symbol_plain ::= sym
left symbol_nsq ::= '/' (sym | coloncolon | dot | dotdash | hat | tilde | and_and | or_or | shift_left | shift_right | not_equal | not_identical | increment | decrement)
literal ::= number | hexnum | binnum | octnum | bool | nil | nan | string | char
  {methods=[getLiteralType getLiteralText]}

access ::= ('.' | '.-') symbol
left access_left ::= ! '.'

metadata ::= "^" (string | symbol | keyword | map)
reader_macro ::= "'" | "~" | "~@" | "`" | "," | ",@"


private meta items ::= <<items_entry <<recover>> <<param>>>> * {recoverWhile="<<recover>>"}
private meta items_entry ::= (not_eof <<recover>>) <<param>>
private list_body ::= <<items !')' form>>

private vec_body ::= <<items !']' form>>
private map_body ::= <<items !'}' map_entry>>
private map_entry ::=  form form {pin=2}